apply plugin: "groovy"

version = "1.0"
description = "Spock Framework - Example Project"

// Spock works with Java 1.8 and above
sourceCompatibility = 1.8

repositories {
  // Spock releases are available from Maven Central
  mavenCentral()
  // Spock snapshots are available from the Sonatype OSS snapshot repository
  maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

dependencies {
  // mandatory dependencies for using Spock
  compile "org.codehaus.groovy:groovy:3.0.7"
  testCompile platform("org.spockframework:spock-bom:2.0-M4-groovy-3.0")
  testCompile "org.spockframework:spock-core"
  testCompile "org.spockframework:spock-junit4"  // you can remove this if your code does not rely on old JUnit 4 rules

  // optional dependencies for using Spock
  testCompile "org.hamcrest:hamcrest-core:2.2"   // only necessary if Hamcrest matchers are used
  testRuntime "net.bytebuddy:byte-buddy:1.10.10" // allows mocking of classes (in addition to interfaces)
  testRuntime "org.objenesis:objenesis:3.1"      // allows mocking of classes without default constructor (together with ByteBuddy or CGLIB)

  // dependencies used by examples in this project
  testRuntime "com.h2database:h2:1.4.197"
  compile "org.codehaus.groovy:groovy-sql:3.0.7"
}

// the remaining configuration is specific to this project, and is not required for using Spock

apply from: "gradle/ide.gradle"

task collectJars(type: Copy) {
    into "$buildDir/output/lib"
    from configurations.testRuntime
}
test {
  useJUnitPlatform()
  testLogging {
    events "passed", "skipped", "failed"
  }
}


